apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

archivesBaseName = 'gwtck'
group = 'com.gmail.kompotik'
version = '0.0.1'

sourceCompatibility = 1.6
targetCompatibility = 1.6

ideaProject {
  javaVersion = '1.6'
}

repositories {
  mavenRepo urls: ["http://repo1.maven.org/maven2"]
  mavenRepo urls: ["http://snapshots.repository.codehaus.org/"]
}

dependencies {
  compile 'com.google.gwt:gwt-servlet:2.4.0'
  compile 'com.google.gwt:gwt-user:2.4.0'
  compile 'com.google.gwt:gwt-dev:2.4.0'
}

dirEditor = 'editor/'
dirWar = 'build/war/'
dirGwtBuild = dirWar + dirEditor

task gwtCompile() << { task ->
  created = (new File(dirGwtBuild)).mkdirs()
  ant.java(classname:'com.google.gwt.dev.Compiler', failOnError: 'true', fork: 'true') {
    jvmarg(value: '-Xss1024k')
    jvmarg(value: '-Xms256M')
    jvmarg(value: '-Xmx256M')
    arg(line: '-war ' + dirGwtBuild)
    arg(line: '-logLevel INFO')
//    arg(line: '-style PRETTY')
//    arg(line: '-localWorkers 2')
//    arg(line: '-treeLogger')
    arg(value: 'com.gmail.kompotik.gwtck.GwtckDevMode')
    classpath {
      pathElement(location: "src/main/java")
      pathElement(path: configurations.compile.asPath)
//      pathElement(path: configurations.compileGwt.asPath)
    }
  }
}

task devmode() << {
  ant.java(classname:'com.google.gwt.dev.DevMode', failOnError: 'true', fork: 'true') {
    jvmarg(value: '-Xss1024k')
    jvmarg(value: '-Xms256M')
    jvmarg(value: '-Xmx256M')
    arg(line: '-war ' + dirWar)
    arg(line: '-startupUrl ' + dirEditor + 'index.html')
    arg(line: '-port 8080')
    arg(value: 'com.gmail.kompotik.gwtck.GwtckDevMode')
    classpath {
      pathElement(location: "src/main/java")
      pathElement(path: configurations.compile.asPath)
//      pathElement(path: configurations.compileGwt.asPath)
    }
  }
}

task copyIndexHtml(type: Copy) {
  from('files') {
    include('index.html')
  }
  into(dirGwtBuild)
}

task copyCkeditorCompiled(type: Copy) {
  from('src/main/resources/com/gmail/kompotik/gwtck/public') {
    include('**/*.*')
  }
  into(dirGwtBuild)
}

task copyGwtXmlSources(type: Copy) {
  from('src/main/java/com/gmail/kompotik/gwtck') {
    include('**/*.xml')
    include('**/*.gwt.xml')
    include('**/*.png')
    include('**/*.java')
  }
  into('build/classes/main/com/gmail/kompotik/gwtck')
}

classes.dependsOn copyGwtXmlSources
gwtCompile.dependsOn classes
devmode.dependsOn gwtCompile
devmode.dependsOn copyCkeditorCompiled
devmode.dependsOn copyIndexHtml

task wrapper(type: Wrapper) {
  gradleVersion = '1.0-milestone-3'
}